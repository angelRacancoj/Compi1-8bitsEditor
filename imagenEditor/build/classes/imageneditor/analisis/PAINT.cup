package imageneditor.analisis;

import java_cup.runtime.*;
import imageneditor.DefaultValue;
import imageneditor.objectsManager.pintarManager;
import imageneditor.exceptions.InputsVaciosException;
import imageneditor.backEnd.AuxObjects.*;
import java.util.LinkedList;

parser code {:

	LinkedList<auxVar> listVar = new LinkedList<>();
	LinkedList<AuxPaint> listPaintVar = new LinkedList<>();

	pintarManager newPaintM;
	int posX 	= DefaultValue.NO_INICIO_DIMENSION;
	int posXint = DefaultValue.NO_INICIO_DIMENSION;
	int posY 	= DefaultValue.NO_INICIO_DIMENSION;
	int posYint = DefaultValue.NO_INICIO_DIMENSION;



	public paint(Lexer lex, pintarManager pntIn){
		super(lex);
		this.newPaintM = pntIn;
	}

	private void setPosX(int xPnt) throws InputsVaciosException {
		if (posX == DefaultValue.NO_INICIO_DIMENSION) {
			posX = xPnt;
		} else {
			throw new InputsVaciosException("Posicion X " + xPnt + " can't set");
		}
	}

	private void setPosXint(int xPnt) throws InputsVaciosException {
		if (posXint == DefaultValue.NO_INICIO_DIMENSION) {
			posXint = xPnt;
		} else {
			throw new InputsVaciosException("Posicion X " + xPnt + " can't set");
		}
	}

	private void setPosY(int yPnt) throws InputsVaciosException {
		if (posY == DefaultValue.NO_INICIO_DIMENSION) {
			posY = yPnt;
		} else {
			throw new InputsVaciosException("Posicion X " + yPnt + " can't set");
		}
	}

	private void setPosYint(int yPnt) throws InputsVaciosException {
		if (posYint == DefaultValue.NO_INICIO_DIMENSION) {
			posYint = yPnt;
		} else {
			throw new InputsVaciosException("Posicion X " + yPnt + " can't set");
		}
	}

	private void addInstruccion(String owner) throws InputsVaciosException {
		newPaintM.addInstruccion(owner);
		for (int i = 0; i< listVar.size() ; i++) {
			newPaintM.addVariableToinstruccion(owner, listVar.get(i).getName());
			newPaintM.changeValVariable(listVar.get(i).getName(),listVar.get(i).getValue());
		}
		for (int j = 0; j < listPaintVar.size() ; j++ ) {
			newPaintM.addPintarInstruccion(owner, listPaintVar.get(j).getName(), listPaintVar.get(j).getPosX() , listPaintVar.get(j).getPosXemd(), listPaintVar.get(j).getPosY() , listPaintVar.get(j).getPosYemd());
		}
		clearAll();
	}

	private String valorSuma(String val1, String val2){
		return String.valueOf(Integer.parseInt(val1) + Integer.parseInt(val2));
	}

	private String valorResta(String val1, String val2){
		return String.valueOf(Integer.parseInt(val1) - Integer.parseInt(val2));
	}

	public void syntax_error(Symbol cur_token) {
		String message = "Estructura invalida en posiciÃ³n " + cur_token.right
			 + ", token: " + cur_token.value;
		System.out.println(">>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>><<<<<");	 
		System.out.println(message);
		System.out.println("<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<");
	}

	private void clearPosXY(){
		posX 	= DefaultValue.NO_INICIO_DIMENSION;
		posXint = DefaultValue.NO_INICIO_DIMENSION;
		posY 	= DefaultValue.NO_INICIO_DIMENSION;
		posYint = DefaultValue.NO_INICIO_DIMENSION;
	}

	private void clearAll(){
		listVar.clear();
		listPaintVar.clear();
		clearPosXY();
	}

:}

/* Terminal */
terminal String Pintar, Vars, Colores, Dimension, Cuadro, Hex, Tipo, Fondo,Red, Blue, Green; 
terminal String Tamanio, Nombre, Lienzos, Igual, Menos, Mas, Coma, Intervalo, DosPuntos, PuntoYComa; 
terminal String ParAbierto, ParCerrado, CorAbierto, CorCerrado, LlaveAbierta, LlaveCerrada;
terminal String Comment, HexaCode, Cadena, Identificador, NewVar, Instrucciones,Entero;
/* Non terminal */
non terminal String PAINT, VARIABLES, INSTRUCCIONS, SET_VARIABLE, DATA, MATH_OP, INT_BODY, POS_X, POS_Y, PAINT_COM, VAR_VALUE, NEW_VAR;

precedence left Mas, Menos;

start with PAINT;

PAINT		::= VARIABLES INSTRUCCIONS PAINT
				| INSTRUCCIONS
				;

VARIABLES	::= Vars LlaveAbierta SET_VARIABLE LlaveCerrada;

SET_VARIABLE	::= NewVar DATA PuntoYComa SET_VARIABLE
					| NewVar DATA PuntoYComa	
					| error:e {: System.out.println("error: "+e);:}
					;

DATA		::= NEW_VAR Coma DATA
				| NEW_VAR
				;

NEW_VAR		::= Identificador:e 								{: newPaintM.addVariable(e); :}
				| Identificador:e1 Igual MATH_OP:e2 			{: newPaintM.addVariable(e1, Integer.parseInt(e2)); :}
				;

MATH_OP		::= MATH_OP:e1 Mas MATH_OP:e2 						{: RESULT = valorSuma(e1, e2); :}
				| MATH_OP:e1 Menos MATH_OP:e2 					{: RESULT = valorResta(e1, e2); :}
				| Identificador:e 								{: RESULT = String.valueOf(newPaintM.varValue(e)); :}
				| Entero:e 										{: RESULT = e; :}
				;

INSTRUCCIONS	::= Instrucciones ParAbierto Identificador:e ParCerrado LlaveAbierta INT_BODY LlaveCerrada {: addInstruccion(e); :} 
				| error:e 										{: System.out.println("error: "+e);:}
				;

INT_BODY	::= VAR_VALUE INT_BODY
				| PAINT_COM INT_BODY
				| VAR_VALUE
				| PAINT_COM
				;

VAR_VALUE	::= Identificador:e1 Igual MATH_OP:e2 PuntoYComa 	{: newPaintM.changeValVariable(e1, Integer.parseInt(e2)); :} ;

PAINT_COM	::= Pintar ParAbierto Identificador:e Coma POS_X Coma POS_Y ParCerrado PuntoYComa {: listPaintVar.add(new AuxPaint(e, posX, posXint, posY, posYint)); clearPosXY(); :};

POS_X		::= MATH_OP:e 										{: setPosX(Integer.parseInt(e)); :}
				| MATH_OP:e1 Intervalo MATH_OP:e2 				{: setPosX(Integer.parseInt(e1)); setPosXint(Integer.parseInt(e2)); :}
				;

POS_Y		::= MATH_OP:e 										{: setPosY(Integer.parseInt(e)); :}
				| MATH_OP:e1 Intervalo MATH_OP:e2 				{: setPosY(Integer.parseInt(e1)); setPosYint(Integer.parseInt(e2)); :}
				;